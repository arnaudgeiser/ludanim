plugins {
  id 'net.researchgate.release' version '2.4.0'
  id "com.moowork.node" version "1.0.1"
  id "com.moowork.gulp" version "1.0.1"
  id 'com.craigburke.client-dependencies' version '1.3.1'
  id 'org.hidetake.ssh' version '2.7.0'
}

apply plugin: 'maven'

remotes {
	webServer {
		host = '46.101.214.30'
		user = 'root'
		identity = file(System.properties['user.home']+'/.ssh/id_rsa')
	}
}

uploadArchives {
	repositories {
		mavenDeployer {
			repository(url: "http://archiva.ludine.ch/repository/internal") {
				authentication(userName: archivaUser, password: archivaPassword)
			};
			snapshotRepository(url: "http://archiva.ludine.ch/repository/snapshots") {
				authentication(userName: archivaUser, password: archivaPassword)
			};
			pom.artifactId = 'ludanim'
			pom.groupId = 'ch.waterbead'
		}
	}
}

node {
	version="6.9.2"
	npmVersion="3.10.9"
	download=true
}

task bower(type:Exec) {
	commandLine 'bower','install'
}

task distZip(dependsOn:gulp_build,type: Zip) {
	baseName="ludanim"
	from 'dist'
}

task deploy {
	doLast {
		ssh.run {
			session(remotes.webServer) {
				execute """cd /var/www/html && 
					 mkdir -p ludanim/versions &&
					 cd ludanim/versions &&
					 wget http://archiva.ludine.ch/repository/internal/ch/waterbead/ludanim/${versionToDeploy}/ludanim-${versionToDeploy}.zip -O ${versionToDeploy}.zip &&
					 rm -R ${versionToDeploy}
					 unzip ${versionToDeploy}.zip -d ${versionToDeploy} &&
					 rm ${versionToDeploy}.zip
					 ln -sf ${versionToDeploy} /var/www/html/ludanim/current"""
			}
		}
	}
}

artifacts {
	archives distZip
}

gulp_build.dependsOn 'npmInstall'
gulp_build.dependsOn 'installGulp'
gulp_build.dependsOn bower

afterReleaseBuild.dependsOn uploadArchives

uploadArchives.dependsOn distZip
